APPROACH: $bleeds_main FROM $start_tester_build
COMMAND: Set piece >> from the start of the game, skipping the first fight
DESCRIPTION: $NONE

APPROACH: $bleeds_main FROM $meadow_fight
APPROACH: $bleeds_main FROM $bleeds_trader_hut
APPROACH: $bleeds_main FROM $goblin_skirmish_sneak
APPROACH: $bleeds_main FROM $goblin_skirmish_main
APPROACH: $bleeds_main FROM $pyramid_entrance

// ---
ROOM: $bleeds_main

FLAGS: $IDLE

POS: 37, 98

FIRST_MAP_NAME: Some buildings
FIRST_HINT: There seems to be a village or at least a homestead next to the Pyramid.

MAP_NAME: The Bleeds
HINT: This is a small village close the entrance to the Pyramid.

FIRST_DESCRIPTION:
I finally see it. The Pyramid.

// TODO: add image

Below the Pyramid there's a small village. It huddles around the entrance to the structure. Later, I learn the locals call the settlement “The Bleeds”.

There is a trader's shop here. A mile to the west, I see a pillar of black smoke rising to the sky.

[[CODE]]
c.learnAbout(kbTrader);
c.learnAbout(kbGoblinCampSmoke);
[[ENDCODE]]

[[CODE]]
// Making sure that Tamara is not active by now, in case we skip
// the initial fight during debug.
w.updateActorById(tamaraId, (b) => b.isActive = false);
[[ENDCODE]]

DESCRIPTION: $NONE


// ---
ACTION: $bleeds_main_observe_village
COMMAND: Village >> observe
FOR_LOCATION: $bleeds_main

COMPLETE_SUCCESS_DESCRIPTION:
[[RULESET]]
[[RULE]]
c.hasHappened(evCaravanArrived) && !c.hasHappened(evCaravanDeparted)
[[THEN]]
With the caravan, the village is lively. The villagers have their doors open, talking with each other and with the arrivals.

The talking and commotion is especially vivid near the local trader's building. On the other end of the liveliness spectrum, there's a small dwelling with a porch here that most people ignore. A blind man sits there on the porch, wearing a coat.
[[ENDRULE]]

[[RULE]]
  $DEFAULT
[[THEN]]
At any point I can see at least a few villagers going about their business. They all walk fast and seldom talk to each other. [[IF !c.hasHappened(evQuake1)]]Something bad is happening.[[ENDIF]]

Every door is shut except for two. One is the entrance into the trader's shop. The second open door belongs to a small dwelling with a porch. A blind man sits outside on a stool, wearing a coat.
[[ENDRULE]]
[[ENDRULESET]]

[[CODE]]
c.learnAbout(kbBlindGuide);
[[ENDCODE]]

// ---
ACTION: $bleeds_main_observe_smoke
COMMAND: Smoke >> observe
FOR_LOCATION: $bleeds_main

PREREQUISITES:
!c.hasHappened(evGoblinCampCleared)

COMPLETE_SUCCESS_DESCRIPTION:
The smoke is black as death but the pillar is narrow. Looks like nothing more than a camp fire.

Someone is not afraid to be found.

NOTES:

if c.hasHappened(evSavedSarn), then Sarn is here
